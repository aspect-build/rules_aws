"""Declare runtime dependencies

These are needed for local dev, and users must install them as well.
See https://docs.bazel.build/versions/main/skylark/deploying.html#dependencies
"""

load("@bazel_tools//tools/build_defs/repo:http.bzl", _http_archive = "http_archive")
load("@bazel_tools//tools/build_defs/repo:utils.bzl", "maybe")
load("//aws/private:toolchains_repo.bzl", "PLATFORMS", "toolchains_repo")
load("//aws/private:versions.bzl", "TOOL_VERSIONS")

def http_archive(name, **kwargs):
    maybe(_http_archive, name = name, **kwargs)

# WARNING: any changes in this function may be BREAKING CHANGES for users
# because we'll fetch a dependency which may be different from one that
# they were previously fetching later in their WORKSPACE setup, and now
# ours took precedence. Such breakages are challenging for users, so any
# changes in this function should be marked as BREAKING in the commit message
# and released only in semver majors.
# This is all fixed by bzlmod, so we just tolerate it for now.
def rules_aws_dependencies():
    # The minimal version of bazel_skylib we require
    http_archive(
        name = "bazel_skylib",
        sha256 = "74d544d96f4a5bb630d465ca8bbcfe231e3594e5aae57e1edbf17a6eb3ca2506",
        urls = [
            "https://github.com/bazelbuild/bazel-skylib/releases/download/1.3.0/bazel-skylib-1.3.0.tar.gz",
            "https://mirror.bazel.build/github.com/bazelbuild/bazel-skylib/releases/download/1.3.0/bazel-skylib-1.3.0.tar.gz",
        ],
    )

########
# Remaining content of the file is only used to support toolchains.
########
_DOC = "Fetch external tools needed for aws toolchain"
_ATTRS = {
    "aws_version": attr.string(mandatory = True, values = TOOL_VERSIONS.keys()),
    "platform": attr.string(mandatory = True, values = PLATFORMS.keys()),
}

def _release_info(rctx):
    release_info = TOOL_VERSIONS[rctx.attr.aws_version][rctx.attr.platform]
    return {
        "url": "https://awscli.amazonaws.com/" + release_info[0].format(rctx.attr.aws_version),
        "integrity": release_info[1],
    }

def _install_linux(rctx):
    rctx.download_and_extract(
        url = _release_info(rctx)["url"],
        integrity = _release_info(rctx)["integrity"],
        stripPrefix = "aws",
    )
    result = rctx.execute(["./install", "--install-dir", "installed"])
    if result.return_code:
        fail("aws CLI installer failed.\nSTDOUT: {}\nSTDERR: {}".format(result.stdout, result.stderr))
    return rctx.path("installed/v2/{}/bin/aws".format(rctx.attr.aws_version))

def _install_darwin(rctx, release_info):
    rctx.download(url = release_info["url"], integrity = release_info["integrity"], output = "AWSCLI.pkg")
    result = rctx.execute(["pkgutil", "--expand-full", "AWSCLI.pkg", "installed"])
    if result.return_code:
        fail("aws CLI unpacking failed.\nSTDOUT: {}\nSTDERR: {}".format(result.stdout, result.stderr))
    return rctx.path("installed/aws-cli.pkg/Payload/aws-cli/aws")

def _aws_repo_impl(rctx):
    if rctx.attr.platform.startswith("linux"):
        target_tool_path = _install_linux(rctx, _release_info(rctx))
    elif rctx.attr.platform == "darwin":
        target_tool_path = _install_darwin(rctx, _release_info(rctx))
    else:
        fail("Unexpected fall-through choosing install method, please file a bug.")

    build_content = """\
# Generated by aws/repositories.bzl
load("@aspect_rules_aws//aws:toolchain.bzl", "aws_toolchain")
aws_toolchain(name = "aws_toolchain", target_tool_path = "{}")
""".format(target_tool_path)

    # Base BUILD file for this repository
    rctx.file("BUILD.bazel", build_content)

aws_repositories = repository_rule(
    _aws_repo_impl,
    doc = _DOC,
    attrs = _ATTRS,
)

# Wrapper macro around everything above, this is the primary API
def aws_register_toolchains(name, register = True, **kwargs):
    """Convenience macro for users which does typical setup.

    - create a repository for each built-in platform like "aws_linux_amd64" -
      this repository is lazily fetched when node is needed for that platform.
    - TODO: create a convenience repository for the host platform like "aws_host"
    - create a repository exposing toolchains for each platform like "aws_platforms"
    - register a toolchain pointing at each platform
    Users can avoid this macro and do these steps themselves, if they want more control.
    Args:
        name: base name for all created repos, like "aws1_14"
        register: whether to call through to native.register_toolchains.
            Should be True for WORKSPACE users, but false when used under bzlmod extension
        **kwargs: passed to each node_repositories call
    """
    for platform in PLATFORMS.keys():
        aws_repositories(
            name = name + "_" + platform,
            platform = platform,
            **kwargs
        )
        if register:
            native.register_toolchains("@%s_toolchains//:%s_toolchain" % (name, platform))

    toolchains_repo(
        name = name + "_toolchains",
        user_repository_name = name,
    )
